cmake_minimum_required(VERSION 3.16)
project(Empire LANGUAGES C CXX)

if(MSVC AND NOT CMAKE_GENERATOR MATCHES "Ninja")
    add_compile_options(/MP)
endif()

if(SDL_STATIC AND NOT SDL_SHARED)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
endif()

set(SDL_TEST_LIBRARY OFF CACHE BOOL "" FORCE)
set(SDL_TESTS OFF CACHE BOOL "" FORCE)
set(SDL_EXAMPLES OFF CACHE BOOL "" FORCE)
set(SDL_DISABLE_INSTALL ON CACHE BOOL "" FORCE)
add_subdirectory(soy/SDL)

set(EMPIRE_GENERATED
    ${CMAKE_SOURCE_DIR}/src/generated/assets_generated.c
    ${CMAKE_SOURCE_DIR}/include/Empire/generated/assets_generated.h
)

foreach(gen_file ${EMPIRE_GENERATED})
    if(NOT EXISTS ${gen_file})
        get_filename_component(gen_dir ${gen_file} DIRECTORY)
        file(MAKE_DIRECTORY ${gen_dir})
        file(WRITE ${gen_file} "// Generated by Florence\n")
    endif()
endforeach()


# Codegen tool (native only)
if(NOT EMSCRIPTEN)
    add_executable(Florence src/codegen/florence.c src/lz4.c)
    target_include_directories(Florence PRIVATE include)
    target_link_libraries(Florence PRIVATE SDL3::SDL3)

    # Always build Florence in Release mode
    set_target_properties(Florence PROPERTIES
        VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
        MSVC_RUNTIME_LIBRARY "MultiThreaded"
        EXCLUDE_FROM_ALL FALSE
    )
    set_property(TARGET Florence PROPERTY INTERPROCEDURAL_OPTIMIZATION_RELEASE TRUE)
    if(CMAKE_BUILD_TYPE STREQUAL "Release" OR CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
        target_compile_definitions(Florence PRIVATE FLORENCE_PACKAGE_ASSETS)
    endif()

    add_custom_target(RunFlorence ALL
        COMMAND Florence
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        DEPENDS Florence
        BYPRODUCTS ${EMPIRE_GENERATED}
        COMMENT "Running Florence codegen..."
    )
endif()

set(EMPIRE_SOURCES
    src/assets.c
    src/entities.c
    src/level.c
    src/lz4.c
    src/main.c
    src/text.c
    src/ui.c
    src/util.c
    src/yyjson.c
)

# compiled with no warnings/errors
set(EMPIRE_EXTERNAL
    src/external.c
)

set(EMPIRE_HEADERS
    include/Empire/aspect.h
    include/Empire/assets.h
    include/Empire/hash.inl
    include/Empire/level.h
    include/Empire/lz4.h
    include/Empire/math.inl
    include/Empire/miniaudio.h
    include/Empire/prototypes.h
    include/Empire/stb_ds.h
    include/Empire/stb_image.h
    include/Empire/stb_truetype.h
    include/Empire/text.h
    include/Empire/types.h
    include/Empire/ui.h
    include/Empire/util.h
    include/Empire/yyjson.h
    src/entities.h
)

add_executable(Empire
    ${EMPIRE_SOURCES}
    ${EMPIRE_EXTERNAL}
    ${EMPIRE_HEADERS}
    ${EMPIRE_GENERATED}
)

set_source_files_properties(${EMPIRE_EXTERNAL} PROPERTIES
    COMPILE_OPTIONS "$<$<C_COMPILER_ID:MSVC>:/W0>$<$<NOT:$<C_COMPILER_ID:MSVC>>:-w>"
)

target_include_directories(Empire PRIVATE include)
target_link_libraries(Empire PRIVATE SDL3::SDL3)

if(CMAKE_BUILD_TYPE STREQUAL "Release" OR CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
    target_compile_definitions(Empire PRIVATE FLORENCE_PACKAGE_ASSETS)
endif()

if(EMSCRIPTEN)
    target_link_options(Empire PRIVATE "--preload-file" "${CMAKE_CURRENT_SOURCE_DIR}/assets@/assets")
endif()

set_target_properties(Empire PROPERTIES COMPILE_WARNING_AS_ERROR ON)

target_compile_options(Empire PRIVATE
    $<$<CXX_COMPILER_ID:MSVC>:
        /W4
        /permissive-
        /wd4100
    >

    $<$<CXX_COMPILER_ID:Clang,AppleClang>:
        -Wall
        -Wshadow
    >
)

# make pretty filters in VS
if(CMAKE_GENERATOR MATCHES "Visual Studio")
    source_group("Sources" FILES ${EMPIRE_SOURCES})
    source_group("Headers" FILES ${EMPIRE_HEADERS})
    if(NOT EMSCRIPTEN)
        source_group("Sources\\Generated" FILES ${CMAKE_SOURCE_DIR}/src/generated/assets_generated.c)
        source_group("Headers\\Generated" FILES ${CMAKE_SOURCE_DIR}/include/Empire/generated/assets_generated.h)
    endif()
endif()

if(NOT EMSCRIPTEN)
    add_dependencies(Empire RunFlorence)
    set_target_properties(Empire PROPERTIES VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}")
endif()

# Copy SDL3 DLL for Windows builds
if(WIN32 AND NOT EMSCRIPTEN)
    add_custom_command(TARGET Florence POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
        $<TARGET_FILE:SDL3::SDL3>
        $<TARGET_FILE_DIR:Florence>
    )
    add_custom_command(TARGET Empire POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
        $<TARGET_FILE:SDL3::SDL3>
        $<TARGET_FILE_DIR:Empire>
    )
endif()

if(EMSCRIPTEN)
    set_target_properties(Empire PROPERTIES SUFFIX ".html")
    target_link_options(Empire PRIVATE
        "-sALLOW_MEMORY_GROWTH=1"
        "-sUSE_SDL=3"
        "-g3"                                       # Level 3 debug: Includes source maps and line info
        "-sASSERTIONS=1"                            # Turns on Emscripten's internal runtime checks
        "-sSTACK_OVERFLOW_CHECK=2"                  # Catches stack errors (common in WASM)
        "--source-map-base http://localhost:8080/"  # Helps the browser find the source files)
    )
endif()
